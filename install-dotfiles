#!/usr/bin/env perl
use strict;
use warnings;
use utf8;

# gnu_compat: --opt="..." is allowed.
# no_bundling: single character option is not bundled.
# no_ignore_case: no ignore case on long option.
use Getopt::Long qw(:config gnu_compat no_bundling no_ignore_case);
use FindBin ();
use Pod::Usage;
use File::Spec::Functions qw(catfile);
use File::Basename qw(dirname);
use Perl6::Say;

use lib "$FindBin::Bin/lib";
use Dotto::Util qw(get_home_from_user determine_user_and_home load_config convert_filename install);


sub usage () {
    pod2usage(-verbose => 1);
}


my $needhelp;
my $home;
my $username;
my $config_file;
my $verbose;
my $force_overwrite = 0;
GetOptions(
    'h|help'
        => \$needhelp,
    'u|username=s'
        => \$username,
    'c|config-file=s'
        => \$config_file,
    'v|verbose'
        => \$verbose,
    'f|force'
        => \$force_overwrite,
    'd|directory=s'
        => \$home,
) or usage;
usage if $needhelp;


if (defined $home) {
    $username = get_user_from_home $home;
}
elsif (defined $username) {
    $home = get_home_from_user $username;
}
else {
    ($username, $home) = determine_user_and_home;
}

if (!defined $config_file && exists $ENV{DOTTORC}) {
    $config_file = $ENV{DOTTORC};
}
my $c = load_config($config_file);
my @files = map { convert_filename $c, $_ } @{$c->{files}};

# For test, check if file exists before installing.
unless ($force_overwrite) {
    my $exists = 0;
    for my $file (@files) {
        my $dest = catfile($home, $file);
        if (-e $dest) {
            warn "install-symlinks: File exists '$dest'.\n";
            $exists = 1;
        }
    }
    exit 1 if $exists;
}

# Install dotfiles to home directory.
# but if path $dest exists even if $force_overwrite == 0
# (maybe problem like race condition), exit with error.
for my $file (@files) {
    my $src  = catfile($c->{directory}, $file);
    my $dest = catfile($home, $file);

    if (-e $dest) {
        unless ($force_overwrite) {
            # checked but file exists?
            die "install-symlinks: File exists '$dest'.\n";
        }
        rmtree($dest);
    }

    say("$src -> $dest") if $verbose;
    install($src, $dest, $username);
}




__END__

=head1 NAME

    install-dotfiles - Install dotfiles to home directory.


=head1 SYNOPSIS

    $ install-dotfiles [OPTIONS]


=head1 OPTIONS

=over 6

=item -h, --help

Show help.

=item -u {user}, --username {user}

Determine home directory from username.

=item -c {config-file}, --config-file {config-file}

Config file path to get dotfiles list.

=item -f, --force

Overwrite dotfiles if they exist.

=item -v, --verbose

Output verbose messages.

=item -d {dir}, --directory {dir}

Directory of home directory.
If you want to specify home directory from username,
use C<-u> option.

=back


=head1 AUTHOR

tyru <tyru.exe@gmail.com>
